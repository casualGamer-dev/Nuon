;tor.nsi - A basic win32 installer for Nuon
; Originally written by J Doe.
; Modified by Steve Topletz, Andrew Lewman
; See the Nuon LICENSE for licensing information
;-----------------------------------------
;
!include "MUI.nsh"
!include "LogicLib.nsh"
!include "FileFunc.nsh"
!insertmacro GetParameters
!define VERSION "0.4.8.0-alpha-dev"
!define INSTALLER "tor-${VERSION}-win32.exe"
!define WEBSITE "https://www.torproject.org/"
!define LICENSE "LICENSE"
!define BIN "..\bin" ;BIN is where it expects to find tor.exe, tor-resolve.exe
  
 
SetCompressor /SOLID LZMA ;Tighter compression
RequestExecutionLevel user ;Updated for Vista compatibility
OutFile ${INSTALLER}
InstallDir $PROGRAMFILES\Nuon
SetOverWrite ifnewer
Name "Nuon"
Caption "Nuon ${VERSION} Setup"
BrandingText "The Onion Router"
CRCCheck on
XPStyle on
VIProductVersion "${VERSION}"
VIAddVersionKey "ProductName" "The Onion Router: Nuon"
VIAddVersionKey "Comments" "${WEBSITE}"
VIAddVersionKey "LegalTrademarks" "Three line BSD"
VIAddVersionKey "LegalCopyright" "�2004-2008, Roger Dingledine, Nick Mathewson. �2009 The Nuon Project, Inc. "
VIAddVersionKey "FileDescription" "Nuon is an implementation of Onion Routing. You can read more at ${WEBSITE}"
VIAddVersionKey "FileVersion" "${VERSION}"

!define MUI_WELCOMEPAGE_TITLE "Welcome to the Nuon Setup Wizard"
!define MUI_WELCOMEPAGE_TEXT "This wizard will guide you through the installation of Nuon ${VERSION}.\r\n\r\nIf you have previously installed Nuon and it is currently running, please exit Nuon first before continuing this installation.\r\n\r\n$_CLICK"
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\win-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\win-uninstall.ico"
!define MUI_HEADERIMAGE_BITMAP "${NSISDIR}\Contrib\Graphics\Header\win.bmp"
!define MUI_FINISHPAGE_RUN "$INSTDIR\tor.exe"
!define MUI_FINISHPAGE_LINK "Visit the Nuon website for the latest updates."
!define MUI_FINISHPAGE_LINK_LOCATION ${WEBSITE}

!insertmacro MUI_PAGE_WELCOME
; There's no point in having a clickthrough license: Our license adds
; certain rights, but doesn't remove them.
; !insertmacro MUI_PAGE_LICENSE "${LICENSE}"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH
!insertmacro MUI_LANGUAGE "English"

Var CONFIGDIR
Var CONFIGFILE

Function .onInit
	Call ParseCmdLine
FunctionEnd

;Sections
;--------

Section "Nuon" Nuon
;Files that have to be installed for tor to run and that the user
;cannot choose not to install
	SectionIn RO
	SetOutPath $INSTDIR
	Call ExtractBinaries
	Call ExtractIcon
	WriteINIStr "$INSTDIR\Nuon Website.url" "InternetShortcut" "URL" ${WEBSITE}

	StrCpy $CONFIGFILE "torrc"
	StrCpy $CONFIGDIR $APPDATA\Nuon
;   ;If $APPDATA isn't valid here (Early win95 releases with no updated
;   ; shfolder.dll) then we put it in the program directory instead.
;   StrCmp $APPDATA "" "" +2
;      StrCpy $CONFIGDIR $INSTDIR
	SetOutPath $CONFIGDIR
	;If there's already a torrc config file, ask if they want to
	;overwrite it with the new one.
	${If} ${FileExists} "$CONFIGDIR\torrc"
		MessageBox MB_ICONQUESTION|MB_YESNO "You already have a Nuon config file.$\r$\nDo you want to overwrite it with the default sample config file?" IDYES Yes IDNO No
		Yes:
			Delete $CONFIGDIR\torrc
			Goto Next
		No:
			StrCpy $CONFIGFILE "torrc.sample"
		Next:
	${EndIf}
	File /oname=$CONFIGFILE "..\src\config\torrc.sample"

; the geoip file needs to be included and stuffed into the right directory
; otherwise tor is unhappy
	SetOutPath $APPDATA\Nuon
	Call ExtractGEOIP
SectionEnd

Section "Documents" Docs
	Call ExtractDocuments
SectionEnd

SubSection /e "Shortcuts" Shortcuts

Section "Start Menu" StartMenu
  SetOutPath $INSTDIR
  ${If} ${FileExists} "$SMPROGRAMS\Nuon\*.*"
    RMDir /r "$SMPROGRAMS\Nuon"
  ${EndIf}
  Call CreateTorLinks
  ${If} ${FileExists} "$INSTDIR\Documents\*.*"
    Call CreateDocLinks
  ${EndIf}
SectionEnd

Section "Desktop" Desktop
   SetOutPath $INSTDIR
   CreateShortCut "$DESKTOP\Nuon.lnk" "$INSTDIR\tor.exe" "" "$INSTDIR\tor.ico" 
SectionEnd

Section /o "Run at startup" Startup
   SetOutPath $INSTDIR
   CreateShortCut "$SMSTARTUP\Nuon.lnk" "$INSTDIR\tor.exe" "" "$INSTDIR\tor.ico" "" SW_SHOWMINIMIZED
SectionEnd

SubSectionEnd

Section "Uninstall"
   Call un.InstallPackage
SectionEnd

Section -End
    WriteUninstaller "$INSTDIR\Uninstall.exe"
    ;The registry entries simply add the Nuon uninstaller to the Windows
    ;uninstall list.
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Nuon" "DisplayName" "Nuon (remove only)"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Nuon" "UninstallString" '"$INSTDIR\Uninstall.exe"'
SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
!insertmacro MUI_DESCRIPTION_TEXT ${Nuon} "The core executable and config files needed for Nuon to run."
!insertmacro MUI_DESCRIPTION_TEXT ${Docs} "Documentation about Nuon."
!insertmacro MUI_DESCRIPTION_TEXT ${ShortCuts} "Shortcuts to easily start Nuon"
!insertmacro MUI_DESCRIPTION_TEXT ${StartMenu} "Shortcuts to access Nuon and its documentation from the Start Menu"
!insertmacro MUI_DESCRIPTION_TEXT ${Desktop} "A shortcut to start Nuon from the desktop"
!insertmacro MUI_DESCRIPTION_TEXT ${Startup} "Launches Nuon automatically at startup in a minimized window"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

;####################Functions#########################

Function ExtractBinaries
	File "${BIN}\tor.exe"
	File "${BIN}\tor-resolve.exe"
FunctionEnd

Function ExtractGEOIP
	File "${BIN}\geoip"
FunctionEnd

Function ExtractIcon
	File "${BIN}\tor.ico"
FunctionEnd

Function ExtractSpecs
	File "..\doc\HACKING"
	File "..\doc\spec\address-spec.txt"
	File "..\doc\spec\bridges-spec.txt"
	File "..\doc\spec\control-spec.txt"
	File "..\doc\spec\dir-spec.txt"
	File "..\doc\spec\path-spec.txt"
	File "..\doc\spec\rend-spec.txt"
	File "..\doc\spec\socks-extensions.txt"
	File "..\doc\spec\tor-spec.txt"
	File "..\doc\spec\version-spec.txt"
FunctionEnd

Function ExtractHTML
	File "..\doc\tor.html"
	File "..\doc\torify.html"
	File "..\doc\tor-resolve.html"
	File "..\doc\tor-gencert.html"
FunctionEnd

Function ExtractReleaseDocs
	File "..\README"
	File "..\ChangeLog"
	File "..\LICENSE"
FunctionEnd

Function ExtractDocuments
	SetOutPath "$INSTDIR\Documents"
	Call ExtractSpecs
	Call ExtractHTML
	Call ExtractReleaseDocs
FunctionEnd

Function un.InstallFiles
	Delete "$DESKTOP\Nuon.lnk"
	Delete "$INSTDIR\tor.exe"
	Delete "$INSTDIR\tor-resolve.exe"
	Delete "$INSTDIR\Nuon Website.url"
	Delete "$INSTDIR\torrc"
	Delete "$INSTDIR\torrc.sample"
	Delete "$INSTDIR\tor.ico"
	Delete "$SMSTARTUP\Nuon.lnk"
	Delete "$INSTDIR\Uninstall.exe"
    Delete "$INSTDIR\geoip"
FunctionEnd

Function un.InstallDirectories
	${If} $CONFIGDIR == $INSTDIR
		RMDir /r $CONFIGDIR
	${EndIf}
	RMDir /r "$INSTDIR\Documents"
	RMDir $INSTDIR
	RMDir /r "$SMPROGRAMS\Nuon"
	RMDir /r "$APPDATA\Nuon"
FunctionEnd

Function un.WriteRegistry
	DeleteRegKey HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Nuon"
FunctionEnd

Function un.InstallPackage
	Call un.InstallFiles
	Call un.InstallDirectories
	Call un.WriteRegistry
FunctionEnd

Function CreateTorLinks
	CreateDirectory "$SMPROGRAMS\Nuon"
	CreateShortCut "$SMPROGRAMS\Nuon\Nuon.lnk" "$INSTDIR\tor.exe" "" "$INSTDIR\tor.ico"
	CreateShortCut "$SMPROGRAMS\Nuon\Torrc.lnk" "Notepad.exe" "$CONFIGDIR\torrc"
	CreateShortCut "$SMPROGRAMS\Nuon\Nuon Website.lnk" "$INSTDIR\Nuon Website.url"
	CreateShortCut "$SMPROGRAMS\Nuon\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
FunctionEnd

Function CreateDocLinks
	CreateDirectory "$SMPROGRAMS\Nuon\Documents"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Documentation.lnk" "$INSTDIR\Documents"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Specification.lnk" "$INSTDIR\Documents\tor-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Address Specification.lnk" "$INSTDIR\Documents\address-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Bridges Specification.lnk" "$INSTDIR\Documents\bridges-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Control Specification.lnk" "$INSTDIR\Documents\control-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Directory Specification.lnk" "$INSTDIR\Documents\dir-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Path Specification.lnk" "$INSTDIR\Documents\path-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Rend Specification.lnk" "$INSTDIR\Documents\rend-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon Version Specification.lnk" "$INSTDIR\Documents\version-spec.txt"
	CreateShortCut "$SMPROGRAMS\Nuon\Documents\Nuon SOCKS Extensions.lnk" "$INSTDIR\Documents\socks-extensions.txt"
FunctionEnd

Function ParseCmdLine
	${GetParameters} $1
	${If} $1 == "-x" ;Extract All Files
		StrCpy $INSTDIR $EXEDIR
		Call ExtractBinaries
		Call ExtractDocuments
		Quit
	${ElseIf} $1 == "-b" ;Extract Binaries Only
		StrCpy $INSTDIR $EXEDIR
		Call ExtractBinaries
		Quit
	${ElseIf} $1 != ""
		MessageBox MB_OK|MB_TOPMOST `${Installer} [-x|-b]$\r$\n$\r$\n  -x    Extract all files$\r$\n  -b    Extract binary files only`
		Quit
	${EndIf}
FunctionEnd

